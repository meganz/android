apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'androidx.navigation.safeargs.kotlin'

// Get the MEGA SDK commit hash using Git
def getSdkGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        workingDir 'src/main/jni/mega/sdk'
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

// Get the MEGAchat (Karere) commit hash using Git
def getKarereGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        workingDir 'src/main/jni/megachat/sdk'
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVerion

    buildFeatures {
        dataBinding = true
        viewBinding = true
    }

    defaultConfig {
        applicationId "mega.privacy.android.app"

        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName "$rootProject.appVersion ($rootProject.versionCode)"

        multiDexEnabled true
        ndk.abiFilters 'armeabi-v7a', 'x86', 'x86_64', 'arm64-v8a'

        buildConfigField("String", "USER_AGENT", "\"MEGAAndroid/${appVersion}_${versionCode}\"")
        resValue("string", "app_version", "\"${versionName}\"")
        resValue("string", "sdk_version", "\"${getSdkGitHash()}\"")
        resValue("string", "karere_version", "\"${getKarereGitHash()}\"")
    }

    sourceSets.main {
        java {
            srcDirs += 'src/main/jni/mega/sdk/bindings/java'
            srcDirs += 'src/main/jni/megachat/sdk/bindings/java'
            exclude '**/MegaApiSwing.java'
        }
        jni.srcDirs = [] //disable automatic ndk-build
        jniLibs.srcDir 'src/main/libs'
        // This is not necessary unless you have precompiled libraries in your project.
    }

    buildTypes {
        debug {
            debuggable true
            //  minifyEnabled false
            //   proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ext.enableCrashlytics = false
        }
        release {
            //  minifyEnabled false
            //  proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        // abortOnError false
    }

    dexOptions {
        jumboMode = true
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        // Sets Java compatibility to Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
//    task megaSDK(type: Exec, description: 'Compile MEGA SDK via NDK') {
//        workingDir 'src/main/jni'
//        commandLine './build.sh', 'all'
//    }
//
//    tasks.withType(JavaCompile) {
//        compileTask -> compileTask.dependsOn megaSDK
//    }

    flavorDimensions "service"
    productFlavors {
        gms {
            dimension "service"
            apply plugin: 'com.google.firebase.crashlytics'
            firebaseCrashlytics {
                // Enable processing and uploading of native symbols to Crashlytics servers.
                // This flag must be enabled to see properly-symbolicated native
                // stack traces in the Crashlytics dashboard.
                nativeSymbolUploadEnabled true
                unstrippedNativeLibsDir 'src/main/obj/local'
            }

        }
        hms {
            dimension "service"
            applicationId = "mega.privacy.android.app.huawei"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

// Desugaring
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.10'

// Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"

// AndroidX
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.3'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.emoji:emoji-appcompat:1.1.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.0'
    implementation 'androidx.fragment:fragment-ktx:1.2.5'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-process:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-service:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-runtime-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'com.google.android.material:material:1.3.0'

// Third-party
    // Hilt
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    kapt "com.google.dagger:hilt-android-compiler:$hiltVersion"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:$hiltAndroidXVersion"
    kapt "androidx.hilt:hilt-compiler:$hiltAndroidXVersion"

    // ExoPlayer
    implementation "com.google.android.exoplayer:exoplayer-core:$exoplayerVersion"
    implementation "com.google.android.exoplayer:exoplayer-ui:$exoplayerVersion"
    implementation(name: "exoplayer-extension-ffmpeg-$exoplayerVersion", ext: 'aar')
    implementation(name: "exoplayer-extension-flac-$exoplayerVersion", ext: 'aar')

    // Other
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.github.nirhart:parallaxscroll:1.0'
    implementation 'com.vdurmont:emoji-java:4.0.0'
    implementation 'com.google.zxing:core:3.4.0'
    implementation 'com.budiyev.android:code-scanner:1.8.3'
    implementation 'me.leolin:ShortcutBadger:1.1.22@aar'
    implementation 'com.brandongogetap:stickyheaders:0.6.1'
    implementation 'com.github.ittianyu:BottomNavigationViewEx:2.0.4'
    implementation 'io.supercharge:shimmerlayout:2.1.0'
    implementation 'net.opacapp:multiline-collapsingtoolbar:27.1.1'
    implementation 'com.github.tony19:named-regexp:0.2.5'
    implementation 'org.hamcrest:hamcrest-library:1.3'
    implementation 'com.google.android:flexbox:2.0.1'
    implementation 'jp.wasabeef:blurry:2.1.0'
    implementation 'com.github.meganz.AndroidDocumentScanner:documentscanner:2.0.8'
    implementation 'org.jetbrains.anko:anko-commons:0.10.8'
    implementation 'com.github.zhpanvip:BannerViewPager:3.4.0'
    implementation 'com.jeremyliao:live-event-bus-x:1.7.3'

    // ReactiveX
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'io.reactivex.rxjava3:rxjava:3.0.4'
    implementation 'io.reactivex.rxjava3:rxkotlin:3.0.1'

    // Fresco
    implementation "com.facebook.fresco:fresco:$frescoVersion"
    implementation "com.facebook.fresco:animated-gif:$frescoVersion"
    implementation "com.facebook.fresco:animated-webp:$frescoVersion"
    implementation "com.facebook.fresco:webpsupport:$frescoVersion"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation files('src/main/libs/libwebrtc.jar')

    // Play Core
    implementation 'com.google.android.play:core:1.10.0'
    implementation 'com.google.android.play:core-ktx:1.8.1'

// Build flavors
    //GMS
    gmsImplementation 'com.google.firebase:firebase-analytics-ktx'
    gmsImplementation 'com.google.firebase:firebase-core:18.0.0'
    gmsImplementation 'com.google.firebase:firebase-crashlytics-ndk'
    gmsImplementation 'com.google.firebase:firebase-messaging:21.0.1'
    gmsImplementation 'com.android.billingclient:billing:3.0.2'
    gmsImplementation 'com.google.android.gms:play-services-ads:19.7.0'
    gmsImplementation 'com.google.android.gms:play-services-location:17.1.0'
    gmsImplementation 'com.google.android.gms:play-services-maps:17.0.0'
    gmsImplementation 'com.google.maps.android:android-maps-utils:0.5'
    gmsImplementation platform('com.google.firebase:firebase-bom:26.7.0')

    //HMS
    hmsImplementation 'com.huawei.hms:iap:4.0.2.300'
    hmsImplementation 'com.huawei.hms:location:4.0.2.300'
    hmsImplementation 'com.huawei.hms:maps:4.0.1.301'
    hmsImplementation 'com.huawei.hms:push:4.0.2.300'
}

def taskRequests = gradle.getStartParameter().getTaskRequests().toString()
if (taskRequests.contains("Hms")) {
    apply plugin: 'com.huawei.agconnect'
} else if (taskRequests.contains("Gms")) {
    apply plugin: 'com.google.gms.google-services'
}
