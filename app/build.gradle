apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "mega.privacy.android.app"
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 46
        versionName "3.0.9"
    }

    sourceSets.main {
        java {
            srcDirs += 'src/main/jni/mega/sdk/bindings/java/nz/mega/sdk'
            exclude '**/MegaApiSwing.java'
        }
        jni.srcDirs = [] //disable automatic ndk-build
        jniLibs.srcDir 'src/main/libs' // This is not necessary unless you have precompiled libraries in your project.
    }

    buildTypes {
        debug {
            //  minifyEnabled false
            //   proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            //  minifyEnabled false
            //  proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        // abortOnError false
    }

    task megaSDK(type: Exec, description: 'Compile MEGA SDK via NDK') {
        workingDir 'src/main/jni'
        commandLine 'make'
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn megaSDK
    }

    task cleanMegaSDK(type: Exec, description: 'Clean MEGA SDK object files') {
        workingDir 'src/main/jni'
        commandLine 'make', 'clean_mega'
    }

    clean.dependsOn 'cleanMegaSDK'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:design:23.3.0'
    compile 'com.google.android.gms:play-services-wallet:8.4.0'
    compile 'com.android.support:recyclerview-v7:23.3.0'
    compile 'com.sothree.slidinguppanel:library:3.3.0'
    compile 'com.github.nirhart:parallaxscroll:1.0'
}
